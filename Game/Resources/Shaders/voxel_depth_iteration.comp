#version 450
#extension GL_EXT_shader_explicit_arithmetic_types         : enable
#extension GL_EXT_shader_explicit_arithmetic_types_int8    : require
#extension GL_EXT_shader_explicit_arithmetic_types_int16   : require
#extension GL_EXT_shader_explicit_arithmetic_types_int32   : require
#extension GL_EXT_shader_explicit_arithmetic_types_int64   : require
#extension GL_EXT_shader_explicit_arithmetic_types_float16 : require
#extension GL_EXT_shader_explicit_arithmetic_types_float32 : require
#extension GL_EXT_shader_explicit_arithmetic_types_float64 : require
#extension GL_EXT_debug_printf : enable

layout (push_constant) uniform SceneInfo {
    layout(row_major) mat4 inverseMVP; // inverse of model-view-projection
    layout(row_major) mat4 mvp; // model * view * projection
    layout(row_major) mat4 mv;  // model * view
    float near;
    float far;
    uint16_t width; // framebuffer(render-target) dimensions
    uint16_t height;
} info;

struct EncodedAABB {
    uint16_t x, y, z;
    uint8_t depth;
    uint8_t flags;
    uint32_t advance;
    uint32_t color;
};

layout (binding=0, rgba32f) uniform image2D positionOutput;
layout (binding=1, rgba8) uniform image2D albedoOutput;
layout (binding=2, rgba8) uniform image2D normalOutput;

layout (std140, binding=3) readonly buffer AABBArray {
    EncodedAABB volumes[];
} data;

struct AABB {
    vec3 min;
    vec3 max;
};

struct Ray {
    vec3 origin;
    vec3 dir;
};

vec3 affineTransform(const vec3 v, const mat4 m) {
    vec4 v4 = vec4(v, 1.0) * m;
    return v4.xyz / v4.w;
}

Ray getRay() {
    float x = (float(gl_GlobalInvocationID.x) + 0.5) / float(info.width);
    float y = (float(gl_GlobalInvocationID.y) + 0.5) / float(info.height);
    x = x * 2.0 - 1.0;
    y = 1.0 - y * 2.0;

    vec3 start = affineTransform(vec3(x, y, 0.0), info.inverseMVP);
    vec3 end = affineTransform(vec3(x, y, 1.0), info.inverseMVP);
    return Ray(start, normalize(end - start));
}

float rayTest(const AABB aabb, const Ray ray) {
    bool inside = true;
    vec3 maxT = vec3(-1, -1, -1);
    vec3 coord = vec3(0, 0, 0);

    for (int i = 0; i < 3; ++i) {
        if (ray.origin[i] < aabb.min[i]) {
            coord[i] = aabb.min[i];
            inside = false;
            if (ray.dir[i] != 0.0) {
                maxT[i] = (aabb.min[i] - ray.origin[i]) / ray.dir[i];
            }
        } else if (ray.origin[i] > aabb.max[i]) {
            coord[i] = aabb.max[i];
            inside = false;
            if (ray.dir[i] != 0.0) {
                maxT[i] = (aabb.max[i] - ray.origin[i]) / ray.dir[i];
            }
        }
    }
    if (inside) return 0.0;

    int plane = 0;
    if (maxT.y > maxT[plane]) plane = 1; // y-axis
    if (maxT.z > maxT[plane]) plane = 2; // z-axis

    if (maxT[plane] < 0.0)
        return -1.0;

    for (int i = 0; i < 3; ++i) {
        if (i != plane) {
            coord[i] = ray.origin[i] + maxT[plane] * ray.dir[i];

            if (coord[i] < aabb.min[i] || coord[i] > aabb.max[i])
                return -1.0;
        }
    }
    return length(coord - ray.origin);
}

float rayTest2(const AABB aabb, const Ray ray) {
    float t1 = (aabb.min.x - ray.origin.x) / ray.dir.x;
    float t2 = (aabb.max.x - ray.origin.x) / ray.dir.x;
    float t3 = (aabb.min.y - ray.origin.y) / ray.dir.y;
    float t4 = (aabb.max.y - ray.origin.y) / ray.dir.y;
    float t5 = (aabb.min.z - ray.origin.z) / ray.dir.z;
    float t6 = (aabb.max.z - ray.origin.z) / ray.dir.z;

    float tmin = max(max(min(t1, t2), min(t3, t4)), min(t5, t6));
    float tmax = min(min(max(t1, t2), max(t3, t4)), max(t5, t6));

    if (tmax < 0.0) {   // box on ray but behind ray origin
        return -1.0;
    }
    if (tmin > tmax) {  // ray doesn't intersect box
        return -1.0;
    }
    return max(tmin, 0.0);
}

AABB normalizedAABB(EncodedAABB e) {
    float x = float(e.x) / 65535.0;
    float y = float(e.y) / 65535.0;
    float z = float(e.z) / 65535.0;
    float ext = 0.5 * pow(0.5, e.depth);
    return AABB(vec3(x-ext, y-ext, z-ext), vec3(x+ext, y+ext, z+ext));
}

struct Sphere {
    vec3 center;
    float radius;
};

float rayTest(const Sphere sphere, const Ray ray) {
    vec3 oc = ray.origin - sphere.center;
    float b = 2.0 * dot(oc, ray.dir);
    float c = dot(oc, oc) - sphere.radius * sphere.radius;
    float discriminant = b*b - 4*c;
    if (discriminant < 0.0) return -1.0;
    return (-b - sqrt(discriminant)) * 0.5;
}

Sphere normalizedSphere(EncodedAABB e) {
    float x = float(e.x) / 65535.0;
    float y = float(e.y) / 65535.0;
    float z = float(e.z) / 65535.0;
    float radius = 0.5 * pow(0.5, e.depth);
    return Sphere(vec3(x, y, z), radius);
}

bool isAABBSmallerThanPixel(const AABB aabb) {
    vec3 pts[8] = {
        {aabb.min.x, aabb.min.y, aabb.min.z},
        {aabb.max.x, aabb.min.y, aabb.min.z},
        {aabb.min.x, aabb.max.y, aabb.min.z},
        {aabb.max.x, aabb.max.y, aabb.min.z},
        {aabb.min.x, aabb.min.y, aabb.max.z},
        {aabb.max.x, aabb.min.y, aabb.max.z},
        {aabb.min.x, aabb.max.y, aabb.max.z},
        {aabb.max.x, aabb.max.y, aabb.max.z},
    };
    for (int i = 0; i < 8; ++i)
        pts[i] = affineTransform(pts[i], info.mvp);

    float minX = pts[0].x;
    float maxX = pts[0].x;
    float minY = pts[0].y;
    float maxY = pts[0].y;

    for (int i = 1; i < 8; ++i) {
        minX = min(pts[i].x, minX);
        minY = min(pts[i].y, minY);
        maxX = max(pts[i].x, maxX);
        maxY = max(pts[i].y, maxY);
    }

    float pixelX = (maxX - minX) * float(info.width - 1) * 0.5;
    float pixelY = (maxY - minY) * float(info.height - 1) * 0.5;
    if (min(pixelX, pixelY) > 1.0)
        return false;
    return true;
}

float rayResultDepth = 1.0;
vec4 rayResultColor = vec4(0);
vec3 rayResultNormal = vec3(0);
uint32_t rayTestCount = 0;
uint32_t maxRayTestCount = 400;

void runTest(const Ray ray) {
    uint32_t index = 0;
    uint32_t count = data.volumes.length();
    while (index < count) {
        rayTestCount += 1;
        const EncodedAABB e = data.volumes[index];
        const AABB aabb = normalizedAABB(e);
        const float t = rayTest2(aabb, ray);
        if (t >= 0.0 && t < rayResultDepth) {
            if (e.flags != 0 || isAABBSmallerThanPixel(aabb)) {  // leaf-node (or small enough)
                const uint32_t p = e.color;
                rayResultColor.r = float(p & 0xff) / 255.0;
                rayResultColor.g = float((p >> 8) & 0xff) / 255.0;
                rayResultColor.b = float((p >> 16) & 0xff) / 255.0;
                rayResultColor.a = float((p >> 24) & 0xff) / 255.0;
                rayResultDepth = t;

                vec3 hitPoint = ray.origin + ray.dir * t;
                vec3 center = (aabb.max + aabb.min) * 0.5;
                vec3 n = hitPoint - center;
                rayResultNormal = normalize(n);

                index += e.advance;
            } else {
                index += 1;
            }
        } else {
            index += e.advance;
        }
    }
}

vec3 boxNormal(vec3 n) {
    vec3 absn = abs(n);
    if (absn.x > absn.y && absn.x > absn.z) {
        n.yz = vec2(0.0);
    } else if (absn.y > absn.x && absn.y > absn.z) {
        n.xz = vec2(0.0);
    } else {
        n.xy = vec2(0.0);
    }
    return normalize(n);
}

float linearDepth(float depth)
{
	float z = depth * 2.0f - 1.0f; 
	return (2.0f * info.near * info.far) / (info.far + info.near - z * (info.far - info.near));	
}

vec3 ndcToAABB(float d) {
    float x = (float(gl_GlobalInvocationID.x) + 0.5) / float(info.width);
    float y = (float(gl_GlobalInvocationID.y) + 0.5) / float(info.height);
    x = x * 2.0 - 1.0;
    y = 1.0 - y * 2.0;
    return affineTransform(vec3(x, y, d), info.inverseMVP);
}

#define WRITE_RAY_ITERATION 1

void main() {
    const Ray ray = getRay();

    vec3 aabbDepthClip = ndcToAABB(1.0);
    float depth = length(aabbDepthClip - ray.origin);
    rayResultDepth = depth;

    runTest(ray);

#if WRITE_RAY_ITERATION
    if (rayTestCount > 0) {
        float c = float(rayTestCount) / float(maxRayTestCount);
        vec4 fragColor = vec4(c,c,c,1.0);
#else
    if (rayResultDepth < depth) {
        vec4 fragColor = rayResultColor;
#endif
        imageStore(albedoOutput, ivec2(gl_GlobalInvocationID.xy), fragColor);

        vec3 normal = boxNormal(rayResultNormal);
        //normal = normal * transpose(inverse(mat3(info.mv)));
        normal = normalize((vec4(normal, 0.0) * info.mv).xyz) * 0.5 + 0.5;
        imageStore(normalOutput, ivec2(gl_GlobalInvocationID.xy), vec4(normal, 1.0));

        vec3 pos = ray.origin + ray.dir * rayResultDepth;
        //float d = linearDepth((vec4(pos, 1.0) * info.mvp).z);
        float d = linearDepth(affineTransform(pos, info.mvp).z);
        pos = vec3(vec4(pos,1.0) * info.mv);

        imageStore(positionOutput, ivec2(gl_GlobalInvocationID.xy), vec4(pos, d));
    } else {
        imageStore(albedoOutput, ivec2(gl_GlobalInvocationID.xy), vec4(0));
        imageStore(normalOutput, ivec2(gl_GlobalInvocationID.xy), vec4(0, 0, 0, 1));
        imageStore(positionOutput, ivec2(gl_GlobalInvocationID.xy), vec4(0, 0, 0, 1));
    }
}
